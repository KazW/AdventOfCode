<!-- livebook:{"persist_outputs":true} -->

# Advent of Code 2024

```elixir
defmodule Helpers do
  def get_input(day, part) do
    input_path = Path.absname(__DIR__) |> Path.join("inputs")
    Path.join(input_path, "day-#{day}-part-#{part}.txt") |> File.read!()
  end
end
```

## Day 1

### Part 1: Historian Hysteria

The Chief Historian is always present for the big Christmas sleigh launch, but nobody has seen him in months! Last anyone heard, he was visiting locations that are historically significant to the North Pole; a group of Senior Historians has asked you to accompany them as they check the places they think he was most likely to visit.

As each location is checked, they will mark it on their list with a star. They figure the Chief Historian **must** be in one of the first fifty places they'll look, so in order to save Christmas, you need to help them get fifty stars on their list before Santa takes off on December 25th.

Collect stars by solving puzzles. Two puzzles will be made available on each day in the Advent calendar; the second puzzle is unlocked when you complete the first. Each puzzle grants one star. Good luck!

You haven't even left yet and the group of Elvish Senior Historians has already hit a problem: their list of locations to check is currently **empty**. Eventually, someone decides that the best place to check first would be the Chief Historian's office.

Upon pouring into the office, everyone confirms that the Chief Historian is indeed nowhere to be found. Instead, the Elves discover an assortment of notes and lists of historically significant locations! This seems to be the planning the Chief Historian was doing before he left. Perhaps these notes can be used to determine which locations to search?

Throughout the Chief's office, the historically significant locations are listed not by name but by a unique number called the **location ID**. To make sure they don't miss anything, The Historians split into two groups, each searching the office and trying to create their own complete list of location IDs.

There's just one problem: by holding the two lists up **side by side** (your puzzle input), it quickly becomes clear that the lists aren't very similar. Maybe you can help The Historians reconcile their lists?

For example:

```
3   4
4   3
2   5
1   3
3   9
3   3
```

Maybe the lists are only off by a small amount! To find out, pair up the numbers and measure how far apart they are. Pair up the *smallest number in the left list* with the **smallest number in the right list**, then the **second-smallest left number** with the **second-smallest right number**, and so on.

Within each pair, figure out **how far apart** the two numbers are; you'll need to **add up all of those distances**. For example, if you pair up a `3` from the left list with a `7` from the right list, the distance apart is `4`; if you pair up a `9` with a `3`, the distance apart is `6`.

In the example list above, the pairs and distances would be as follows:

* The smallest number in the left list is `1`, and the smallest number in the right list is `3`. The distance between them is `2`.
* The second-smallest number in the left list is `2`, and the second-smallest number in the right list is another `3`. The distance between them is `1`.
* The third-smallest number in both lists is `3`, so the distance between them is `0`.
* The next numbers to pair up are `3` and `4`, a distance of `1`.
* The fifth-smallest numbers in each list are `3` and `5`, a distance of `2`.
* Finally, the largest number in the left list is `4`, while the largest number in the right list is `9`; these are a distance `5` apart.

To find the **total distance** between the left list and the right list, add up the distances between all of the pairs you found. In the example above, this is `2 + 1 + 0 + 1 + 2 + 5`, a total distance of `11`!

**Your actual left and right lists contain many location IDs. What is the total distance between your lists?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Day1 do
  @line_splitter "\n"
  @pair_splitter "   "

  @spec get_distance(lists :: binary) :: distance :: non_neg_integer()
  def get_distance(lists)
    when is_binary(lists),
    do:
      lists
      |> parse()
      |> sort_lists(:asc)
      |> zip_lists()
      |> get_distances()
      |> Enum.sum()

  @spec get_similarity(lists :: binary) :: similarity :: non_neg_integer()
  def get_similarity(lists)
    when is_binary(lists),
    do:
      lists
      |> parse()
      |> get_similarities()
      |> Enum.sum()

  @spec parse(lists :: binary) :: {left :: [non_neg_integer()], right :: [non_neg_integer()]}
  def parse(lists)
    when is_binary(lists),
    do:
      lists
      |> String.split(@line_splitter, trim: true)
      |> Enum.map(&parse_pair/1)
      |> Enum.reduce({[], []}, &build_lists/2)
      |> reverse_lists()
  
  defp parse_pair(pair),
    do:
      pair
      |> String.split(@pair_splitter)
      |> Enum.map(&String.to_integer(&1))

  defp build_lists([left_elem, right_elem], {left, right}),
    do: {[left_elem | left], [right_elem | right]}

  defp reverse_lists({left, right}),
    do: {Enum.reverse(left), Enum.reverse(right)}

  defp sort_lists({left, right}, dir),
    do: {Enum.sort(left, dir), Enum.sort(right, dir)}

  defp zip_lists({l, r}),
    do:
      Enum.zip(l, r)
      |> Enum.map(&Tuple.to_list/1)

  defp get_distances(pairs),
    do: Enum.map(pairs, &pair_distance/1)
  
  defp pair_distance([left, right]),
    do: abs(left - right)

  defp get_similarities({left, right}) do
    frequencies = Enum.frequencies(right)
    Enum.map(left, &get_value_similarity(&1, frequencies))
  end

  defp get_value_similarity(value, frequencies),
    do: value * (frequencies[value] || 0)
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day1, <<70, 79, 82, 49, 0, 0, 17, ...>>, {:get_value_similarity, 2}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_1_part_1_test_input = """
3   4
4   3
2   5
1   3
3   9
3   3
"""
day_1_part_1_test_answer = 11
day_1_part_1_test_output = Day1.get_distance(day_1_part_1_test_input)
day_1_part_1_test_output == day_1_part_1_test_answer
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_1_part_1_puzzle_input = Helpers.get_input(1, 1)
day_1_part_1_puzzle_output = Day1.get_distance(day_1_part_1_puzzle_input)
```

<!-- livebook:{"output":true} -->

```
1879048
```

### Part 2

Your analysis only confirmed what everyone feared: the two lists of location IDs are indeed very different.

Or are they?

The Historians can't agree on which group made the mistakes or how to read most of the Chief's handwriting, but in the commotion you notice an interesting detail: a lot of location IDs appear in both lists! Maybe the other numbers aren't location IDs at all but rather misinterpreted handwriting.

This time, you'll need to figure out exactly how often each number from the left list appears in the right list. Calculate a total **similarity score** by adding up each number in the left list after multiplying it by the number of times that number appears in the right list.

Here are the same example lists again:

```
3   4
4   3
2   5
1   3
3   9
3   3
```

For these example lists, here is the process of finding the similarity score:

* The first number in the left list is `3`. It appears in the right list three times, so the similarity score increases by `3 * 3 = 9`.
* The second number in the left list is `4`. It appears in the right list once, so the similarity score increases by `4 * 1 = 4`.
* The third number in the left list is `2`. It does not appear in the right list, so the similarity score does not increase (`2 * 0 = 0`).
* The fourth number, `1`, also does not appear in the right list.
* The fifth number, `3`, appears in the right list three times; the similarity score increases by `9`.
* The last number, `3`, appears in the right list three times; the similarity score again increases by `9`.

So, for these example lists, the similarity score at the end of this process is `31` (`9 + 4 + 0 + 0 + 9 + 9`).

Once again consider your left and right lists. **What is their similarity score?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_1_part_2_test_answer = 31
day_1_part_2_test_output = Day1.get_similarity(day_1_part_1_test_input)
day_1_part_2_test_output == day_1_part_2_test_answer
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_1_part_2_puzzle_output = Day1.get_similarity(day_1_part_1_puzzle_input)
```

<!-- livebook:{"output":true} -->

```
21024792
```

## Day 2

### Part 1: Red-Nosed Reports

Fortunately, the first location The Historians want to search isn't a long walk from the Chief Historian's office.

While the Red-Nosed Reindeer nuclear fusion/fission plant appears to contain no sign of the Chief Historian, the engineers there run up to you as soon as they see you. Apparently, they still talk about the time Rudolph was saved through molecular synthesis from a single electron.

They're quick to add that - since you're already here - they'd really appreciate your help analyzing some unusual data from the Red-Nosed reactor. You turn to check if The Historians are waiting for you, but they seem to have already divided into groups that are currently searching every corner of the facility. You offer to help with the unusual data.

The unusual data (your puzzle input) consists of many **reports**, one report per line. Each report is a list of numbers called **levels** that are separated by spaces. For example:

```
7 6 4 2 1
1 2 7 8 9
9 7 6 2 1
1 3 2 4 5
8 6 4 4 1
1 3 6 7 9
```

This example data contains six reports each containing five levels.

The engineers are trying to figure out which reports are safe. The Red-Nosed reactor safety systems can only tolerate levels that are either gradually increasing or gradually decreasing. So, a report only counts as **safe** if both of the following are true:

The levels are either **all increasing** or **all decreasing**.
Any two adjacent levels differ by **at least one** and **at most three**.
In the example above, the reports can be found safe or unsafe by checking those rules:

* `7 6 4 2 1`: **Safe** because the levels are all decreasing by `1` or `2`.
* `1 2 7 8 9`: **Unsafe** because `2` `7` is an increase of 5.
* `9 7 6 2 1`: **Unsafe** because `6` `2` is a decrease of `4`.
* `1 3 2 4 5`: **Unsafe** because `1` `3` is increasing but `3` `2` is decreasing.
* `8 6 4 4 1`: **Unsafe** because `4` `4` is neither an increase or a decrease.
* `1 3 6 7 9`: **Safe** because the levels are all increasing by `1`, `2`, or `3`.

So, in this example, `2` reports are **safe**.

Analyze the unusual data from the engineers. **How many reports are safe?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Day2 do
  @line_splitter "\n"
  @report_splitter " "
  @safe "Safe"
  @unsafe "Unsafe"

  def get_safe_count(reports, dampen)
    when is_binary(reports) and is_boolean(dampen),
    do:
      reports
      |> get_safety_statum(dampen)
      |> Enum.filter(fn status -> status == @safe end)
      |> length()

  def get_safety_statum(reports, dampen)
    when is_binary(reports) and is_boolean(dampen),
    do:
      reports
      |> parse()
      |> Enum.map(&get_levels_safety(&1, dampen))

  def parse(reports)
    when is_binary(reports),
    do:
      reports
      |> String.split(@line_splitter, trim: true)
      |> Enum.map(&parse_report/1)

  defp parse_report(line),
    do:
      line
      |> String.split(@report_splitter, trim: true)
      |> Enum.map(&String.to_integer/1)

  defp get_levels_safety(levels, dampen),
    do:
      levels
      |> Enum.chunk_every(2, 1, :discard)
      |> Enum.with_index()
      |> Enum.map(&evaluate_level_pair/1)
      |> determine_report_safety(dampen, levels)

  defp evaluate_level_pair({pair = [first, second], index}),
    do:
      {
        get_level_pair_direction(first, second),
        is_level_change_in_range(first, second),
        pair,
        index
      }

  defp get_level_pair_direction(first, second) do
    cond do
      first > second -> :decreasing
      first < second -> :increasing
      first == second -> :equal
    end
  end

  defp is_level_change_in_range(first, second),
    do: abs(first - second) in 1..3

  defp determine_report_safety(report, false, _levels) do
    directions = get_report_directions(report)
    ranges = get_report_ranges(report)
    {_direction, directions_result} =
      Enum.reduce(directions, {nil, nil}, &report_direction_reducer/2)
    
    if directions_result && Enum.all?(ranges) do
      @safe
    else
      @unsafe
    end
  end

  defp determine_report_safety(report, true, levels) do
    if determine_report_safety(report, false, levels) == @safe do
      @safe
    else
      out_of_range_positions =
        report
        |> Enum.map(fn {_dir, in_range, _pair, index} -> {in_range, index} end)
        |> Enum.filter(fn {in_range, _index} -> !in_range end)
        |> Enum.map(fn {_in_range, index} -> [index, index + 1] end)
        |> List.flatten()

      direction_frequencies = get_report_directions(report) |> Enum.frequencies()

      {main_direction, _count} =
        direction_frequencies
        |> Enum.max_by(fn {_direction, count} -> count end)

      other_directions =
        direction_frequencies
        |> Map.keys()
        |> Enum.reject(fn direction -> direction == main_direction end)

      wrong_direction_positions =
        report
        |> Enum.map(fn {direction, _in_range, _pair, index} -> {direction, index} end)
        |> Enum.filter(fn {direction, _index} -> direction in other_directions end)
        |> Enum.map(fn {_direction, index} -> [index, index + 1] end)
        |> List.flatten()

      test_positions =
        out_of_range_positions ++ wrong_direction_positions
        |> Enum.uniq()

      tests =
        test_positions
        |> Enum.map(fn position ->
          List.delete_at(levels, position) |> get_levels_safety(false)
        end)

      if Enum.any?(tests, fn result -> result == @safe end) do
        @safe
      else
        @unsafe
      end
    end
  end

  defp get_report_directions(report),
    do: report |> Enum.map(fn {dir, _in_range, _pair, _index} -> dir end)

  defp get_report_ranges(report),
    do: report |> Enum.map(fn {_dir, in_range, _pair, _index} -> in_range end)

  defp report_direction_reducer(direction, {nil, nil}),
    do: {direction, true}

  defp report_direction_reducer(current_direction, {first_direction, pass}),
    do: {first_direction, first_direction == current_direction && pass}
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day2, <<70, 79, 82, 49, 0, 0, 31, ...>>, {:report_direction_reducer, 2}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_2_part_1_test_input = """
7 6 4 2 1
1 2 7 8 9
9 7 6 2 1
1 3 2 4 5
8 6 4 4 1
1 3 6 7 9
"""
day_2_part_1_test_answer = 2
day_2_part_1_test_output = Day2.get_safe_count(day_2_part_1_test_input, false)
day_2_part_1_test_output == day_2_part_1_test_answer
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_2_part_1_puzzle_input = Helpers.get_input(2, 1)
day_2_part_1_puzzle_output = Day2.get_safe_count(day_2_part_1_puzzle_input, false)
```

<!-- livebook:{"output":true} -->

```
463
```

### Part 2

The engineers are surprised by the low number of safe reports until they realize they forgot to tell you about the Problem Dampener.

The Problem Dampener is a reactor-mounted module that lets the reactor safety systems **tolerate a single bad level** in what would otherwise be a safe report. It's like the bad level never happened!

Now, the same rules apply as before, except if removing a single level from an unsafe report would make it safe, the report instead counts as safe.

More of the above example's reports are now safe:

* `7 6 4 2 1`: Safe without removing any level.
* `1 2 7 8 9`: Unsafe regardless of which level is removed.
* `9 7 6 2 1`: Unsafe regardless of which level is removed.
* `1 3 2 4 5`: Safe by removing the second level, `3`.
* `8 6 4 4 1`: Safe by removing the third level, `4`.
* `1 3 6 7 9`: Safe without removing any level.

Thanks to the Problem Dampener, `4` reports are actually **safe**!

Update your analysis by handling situations where the Problem Dampener can remove a single level from unsafe reports. **How many reports are now safe?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_2_part_2_test_answer = ["Safe", "Unsafe", "Unsafe", "Safe", "Safe", "Safe"]
day_2_part_2_test_answer_count = 4
day_2_part_2_test_output = Day2.get_safety_statum(day_2_part_1_test_input, true)
day_2_part_2_test_output_count = Day2.get_safe_count(day_2_part_1_test_input, true)

day_2_part_2_test_output_count == day_2_part_2_test_answer_count
  && day_2_part_2_test_answer == day_2_part_2_test_output
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_2_part_2_puzzle_output = Day2.get_safe_count(day_2_part_1_puzzle_input, true)
```

<!-- livebook:{"output":true} -->

```
514
```

## Day 3

### Part 1: Mull It Over

"Our computers are having issues, so I have no idea if we have any Chief Historians in stock! You're welcome to check the warehouse, though," says the mildly flustered shopkeeper at the North Pole Toboggan Rental Shop. The Historians head out to take a look.

The shopkeeper turns to you. "Any chance you can see why our computers are having issues again?"

The computer appears to be trying to run a program, but its memory (your puzzle input) is **corrupted**. All of the instructions have been jumbled up!

It seems like the goal of the program is just to **multiply some numbers**. It does that with instructions like `mul(X,Y)`, where `X` and `Y` are each `1-3` digit numbers. For instance, `mul(44,46)` multiplies `44` by `46` to get a result of `2024`. Similarly, `mul(123,4)` would multiply `123` by `4`.

However, because the program's memory has been *corrupted, there are also many invalid characters that should be **ignored**, even if they look like part of a mul instruction. Sequences like `mul(4*`, `mul(6,9!`, `?(12,34)`, or `mul ( 2 , 4 )` do **nothing**.

For example, consider the following section of corrupted memory:

`xmul(2,4)%&mul[3,7]!@^do_not_mul(5,5)+mul(32,64]then(mul(11,8)mul(8,5))`

Only the four highlighted sections are real `mul` instructions. Adding up the result of each instruction produces `161` (`2*4 + 5*5 + 11*8 + 8*5`).

Scan the corrupted memory for uncorrupted mul instructions. **What do you get if you add up all of the results of the multiplications?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Day3 do
  @mul_regex ~r/mul\((\d+),(\d+)\)/
  @mul_enable_regex ~r/(mul\((\d+),(\d+)\))|do(n't)?\(\)/
  @default_op :mul

  def part_1(input),
    do:
      @mul_regex
      |> Regex.scan(input)
      |> Enum.map(fn [_match, l, r] -> [String.to_integer(l), String.to_integer(r)] end)
      |> Enum.map(fn [l, r] -> l * r end)
      |> Enum.sum()

  def part_2(input),
    do:
      @mul_enable_regex
      |> Regex.scan(input)
      |> Enum.reduce([], &toggle_reducer/2)
      |> Enum.filter(fn {op, l, _} -> !is_nil(l) && op == :mul end)
      |> Enum.map(&modifier/1)
      |> Enum.sum()

  defp toggle_reducer(["do()" | _], acc),
    do: [{:mul, nil, nil} | acc]

  defp toggle_reducer(["don't()" | _], acc),
    do: [{:noop, nil, nil} | acc]

  defp toggle_reducer(["mul(" <> _, _, l, r], acc)
    when length(acc) == 0,
    do: [{@default_op, String.to_integer(l), String.to_integer(r)} | acc]

  defp toggle_reducer(["mul(" <> _, _, l, r], acc) do
    {last_op, _, _} = hd(acc)
    [{last_op, String.to_integer(l), String.to_integer(r)} | acc]
  end

  defp modifier({:mul, l, r}),
    do: l * r
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day3, <<70, 79, 82, 49, 0, 0, 17, ...>>, {:modifier, 1}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_3_part_1_test_input = "xmul(2,4)%&mul[3,7]!@^do_not_mul(5,5)+mul(32,64]then(mul(11,8)mul(8,5))"
day_3_part_1_test_answer = 161
day_3_part_1_test_output = Day3.part_1(day_3_part_1_test_input)
day_3_part_1_test_answer == day_3_part_1_test_output
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_3_part_1_puzzle_input = Helpers.get_input(3, 1)
day_3_part_1_puzzle_output = Day3.part_1(day_3_part_1_puzzle_input)
```

<!-- livebook:{"output":true} -->

```
188116424
```

### Part 2

As you scan through the corrupted memory, you notice that some of the conditional statements are also still intact. If you handle some of the uncorrupted conditional statements in the program, you might be able to get an even more accurate result.

There are two new instructions you'll need to handle:

* The `do()` instruction **enables** future `mul` instructions.
* The `don't()` instruction **disables** future `mul` instructions.

Only the **most recent** `do()` or `don't()` instruction applies. At the beginning of the program, `mul` instructions are **enabled**.

For example:

`xmul(2,4)&mul[3,7]!^don't()_mul(5,5)+mul(32,64](mul(11,8)undo()?mul(8,5))`

This corrupted memory is similar to the example from before, but this time the `mul(5,5)` and `mul(11,8)` instructions are **disabled** because there is a `don't()` instruction before them. The other mul instructions function normally, including the one at the end that gets re-**enabled** by a `do()` instruction.

This time, the sum of the results is `48` (`2*4 + 8*5`).

Handle the new instructions; **what do you get if you add up all of the results of just the enabled multiplications?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_3_part_2_test_input = "xmul(2,4)&mul[3,7]!^don't()_mul(5,5)+mul(32,64](mul(11,8)undo()?mul(8,5))"
day_3_part_2_test_answer = 48
day_3_part_2_test_output = Day3.part_2(day_3_part_2_test_input)
day_3_part_2_test_answer == day_3_part_2_test_output
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_3_part_2_puzzle_output = Day3.part_2(day_3_part_1_puzzle_input)
```

<!-- livebook:{"output":true} -->

```
104245808
```

## Day 4

### Part 1: Ceres Search

"Looks like the Chief's not here. Next!" One of The Historians pulls out a device and pushes the only button on it. After a brief flash, you recognize the interior of the Ceres monitoring station!

As the search for the Chief continues, a small Elf who lives on the station tugs on your shirt; she'd like to know if you could help her with her **word search** (your puzzle input). She only has to find one word: `XMAS`.

This word search allows words to be horizontal, vertical, diagonal, written backwards, or even overlapping other words. It's a little unusual, though, as you don't merely need to find one instance of `XMAS` - you need to find **all of them**. Here are a few ways `XMAS` might appear, where irrelevant characters have been replaced with `.`:

```
..X...
.SAMX.
.A..A.
XMAS.S
.X....
```

The actual word search will be full of letters instead. For example:

```
MMMSXXMASM
MSAMXMSMSA
AMXSXMAAMM
MSAMASMSMX
XMASAMXAMM
XXAMMXXAMA
SMSMSASXSS
SAXAMASAAA
MAMMMXMMMM
MXMXAXMASX
```

In this word search, `XMAS` occurs a total of `18` times; here's the same word search again, but where letters not involved in any `XMAS` have been replaced with `.`:

```
....XXMAS.
.SAMXMS...
...S..A...
..A.A.MS.X
XMASAMX.MM
X.....XA.A
S.S.S.S.SS
.A.A.A.A.A
..M.M.M.MM
.X.X.XMASX
```

Take a look at the little Elf's word search. **How many times does XMAS appear?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
defmodule Day4 do
  @forward ["X", "M", "A", "S"]
  @backward Enum.reverse(@forward)

  @targets ["SAM", "MAS"]
  @target_length 3
  @target_step 1
  @target_action :discard

  def part_1(input) do
    grid = make_grid(input)
    reverse_grid = Enum.reverse(grid)

    [
      count_in_set(grid, @forward),
      count_in_set(grid, @backward),
      count_verticals(grid, @forward),
      count_verticals(grid, @backward),
      count_diagonals(grid, @forward),
      count_diagonals(grid, @backward),
      count_diagonals(reverse_grid, @forward),
      count_diagonals(reverse_grid, @backward),
    ]
    |> Enum.sum()
  end

  def make_grid(input),
    do:
      input
      |> String.split("\n", trim: true)
      |> Enum.map(&String.split(&1, "", trim: true))

  defp count_in_set(set, target),
    do:
      set
      |> Enum.map(&count_in_line(&1, target))
      |> Enum.sum()

  defp count_verticals(grid, target),
    do:
      grid
      |> get_verticals()
      |> count_in_set(target)

  defp count_diagonals(grid, target),
    do:
      grid
      |> get_diagonals()
      |> count_in_set(target)

  defp count_in_line(line, target),
    do: 
      line
      |> Enum.join()
      |> String.split(Enum.join(target))
      |> length()
      |> Kernel.-(1)

  def get_verticals(grid),
    do:
      0..(length(grid) - 1)
      |> Enum.map(fn i -> Enum.map(grid, fn line -> Enum.at(line, i) end) end)

  def get_diagonals(grid),
    do:
      1..((length(grid) * 2) - 1)
      |> Enum.map(&get_diagonal_line(&1, grid))
      |> Enum.map(fn line -> Enum.map(line, &get_grid_point(&1, grid)) end)

  defp get_diagonal_line(index, grid) do
    clamp = length(grid)
    
    size = cond do
      index <= clamp -> index
      index > clamp -> clamp + (clamp - index)
    end

    1..size
    |> Enum.map(&make_cords(index, &1, clamp))
  end

  defp make_cords(diagonal_index, line_index, clamp)
    when diagonal_index <= clamp,
    do: {line_index - 1, diagonal_index - line_index}

  defp make_cords(diagonal_index, line_index, clamp)
    when diagonal_index > clamp,
    do: {diagonal_index - clamp + line_index - 1, clamp - line_index}

  defp get_grid_point({x, y}, grid),
    do: grid |> Enum.at(y) |> Enum.at(x)

  def part_2(input),
    do:
      input
      |> make_grid()
      |> subgrid(@target_length, @target_step, @target_action)
      |> Enum.map(&get_x/1)
      |> Enum.map(fn [x, y] -> x in @targets && y in @targets end)
      |> Enum.filter(fn res -> res end)
      |> length()

  def subgrid(grid, size, step, action),
    do:
      grid
      |> Enum.chunk_every(size, step, action)
      |> Enum.map(fn chunk ->
        0..((chunk |> hd() |> length()) - 1)
        |> Enum.chunk_every(size, step, action)
        |> Enum.map(fn grid_indexes ->
          chunk
          |> Enum.map(fn row ->
            Enum.map(grid_indexes, fn i -> Enum.at(row, i) end)
          end)
        end)
      end)
      |> Enum.reduce([], fn set, acc -> acc ++ set end)

  defp get_x([[s1, _, s3],[_,s5,_],[s7,_,s9]]),
    do: ["#{s1}#{s5}#{s9}", "#{s3}#{s5}#{s7}"]
end
```

<!-- livebook:{"output":true} -->

```
{:module, Day4, <<70, 79, 82, 49, 0, 0, 30, ...>>, {:get_x, 1}}
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_4_part_1_test_input = """
MMMSXXMASM
MSAMXMSMSA
AMXSXMAAMM
MSAMASMSMX
XMASAMXAMM
XXAMMXXAMA
SMSMSASXSS
SAXAMASAAA
MAMMMXMMMM
MXMXAXMASX
"""
day_4_part_1_test_answer = 18
day_4_part_1_test_output = Day4.part_1(day_4_part_1_test_input)
day_4_part_1_test_answer == day_4_part_1_test_output
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_4_part_1_puzzle_input = Helpers.get_input(4, 1)
day_4_part_1_puzzle_output = Day4.part_1(day_4_part_1_puzzle_input)
```

<!-- livebook:{"output":true} -->

```
2397
```

### Part Two

The Elf looks quizzically at you. Did you misunderstand the assignment?

Looking for the instructions, you flip over the word search to find that this isn't actually an `XMAS` puzzle; it's an `X-MAS` puzzle in which you're supposed to find two `MAS` in the shape of an `X`. One way to achieve that is like this:

```
M.S
.A.
M.S
```

Irrelevant characters have again been replaced with `.` in the above diagram. Within the `X`, each MAS can be written forwards or backwards.

Here's the same example from before, but this time all of the `X-MAS`es have been kept instead:

```
.M.S......
..A..MSMS.
.M.S.MAA..
..A.ASMSM.
.M.S.M....
..........
S.S.S.S.S.
.A.A.A.A..
M.M.M.M.M.
..........
```

In this example, an `X-MAS` appears `9` times.

Flip the word search from the instructions back over to the word search side and try again. **How many times does an `X-MAS` appear?**

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
day_4_part_2_test_answer = 9
day_4_part_2_test_output = Day4.part_2(day_4_part_1_test_input)
day_4_part_2_test_answer == day_4_part_2_test_output
```

<!-- livebook:{"output":true} -->

```
true
```

<!-- livebook:{"reevaluate_automatically":true} -->

```elixir
Day4.part_2(day_4_part_1_puzzle_input) 
```

<!-- livebook:{"output":true} -->

```
1824
```
